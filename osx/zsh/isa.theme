# vim: ft=zsh
setopt prompt_subst
autoload -U colors && colors
autoload -Uz vcs_info

zstyle ':vcs_info:*' stagedstr '%F{green} ●%f'
zstyle ':vcs_info:*' unstagedstr '%F{yellow} ●%f'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{yellow} :: %f%F{yellow}%r%f'
zstyle ':vcs_info:*' enable git svn hg

precmd () {
   if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]] {
      zstyle ':vcs_info:*' formats '%F{yellow}|%f %F{green}%b%c%u%f'
   } else {
      zstyle ':vcs_info:*' formats '%F{yellow}|%f %F{green}%b%c%u%f%F{red} ●%f%F{white}%f'
   }
   vcs_info
}

function prompt_char {
   WARN="%{$fg[green]%}"
   if test "$UID" = 0; then
      WARN="%{$fg[red]%}"
   fi

   git branch >/dev/null 2>/dev/null && echo "${WARN}±" && return
   hg root >/dev/null 2>/dev/null && echo "${WARN}☿" && return
   svn info >/dev/null 2>/dev/null && echo "${WARN}⚡" && return
   echo "${WARN}λ"
}

PROMPT='%{$fg[blue]%}%n%{$reset_color%} in %{$fg[yellow]%}${PWD/#$HOME/~} ${vcs_info_msg_0_}%{$reset_color%}
%(!.%{$fg[green]%}.%{$fg[red]%})$(prompt_char) %{$reset_color%}'
RPROMPT='%{$reset_color%}%t%{$fg[reset_color]%}'

# -- Loop prompt --
PROMPT2='{%_}  '

# -- Selection prompt --
PROMPT3='{ … }  '

